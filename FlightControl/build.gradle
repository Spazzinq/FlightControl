plugins {
    id 'io.freefair.lombok' version '8.6'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

dependencies {
    implementation project(':API')
    implementation project(':Multiversion')
    implementation project(':VersionOld')
    implementation project(':VersionCurrent')

    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation(platform("com.intellectualsites.bom:bom-newest:1.40"))

    // Latest Spigot version
    compileOnly ('io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT')

    // Placeholders
    compileOnly ('me.clip:placeholderapi:2.11.5') { exclude(group: "*") } // https://github.com/PlaceholderAPI/PlaceholderAPI/releases/tag/2.11.5

    // Vanish
    compileOnly ('net.essentialsx:EssentialsX:2.20.1') { exclude(group: "*") }

    // Territories
    compileOnly ('com.sk89q.worldedit:worldedit-bukkit:7.3.0-SNAPSHOT') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    compileOnly ("com.intellectualsites.plotsquared:plotsquared-core") { exclude(group: "*") }
    compileOnly ("com.intellectualsites.plotsquared:plotsquared-bukkit") { exclude(group: "*") }
    compileOnly ('com.palmergames.bukkit.towny:towny:0.100.0.20') { exclude(group: "*") } // https://github.com/TownyAdvanced/Towny
    compileOnly ('com.github.Angeschossen:LandsAPI:6.42.15') { exclude(group: "*") } // https://github.com/Angeschossen/LandsAPI
    compileOnly ('com.github.TechFortress:GriefPrevention:17.0.0') { exclude(group: "*") } // https://github.com/TechFortress/GriefPrevention/
    compileOnly ('com.griefdefender:api:2.1.0-SNAPSHOT') { exclude(group: "*") } // https://github.com/bloodmc/GriefDefenderAPI
    compileOnly ('world.bentobox:bentobox:2.0.0-SNAPSHOT') { exclude(group: "*") } // https://github.com/bentoboxworld
    compileOnly ('net.william278:HuskTowns:2.6.1') { exclude(group: "*") } // https://github.com/WiIIiam278/HuskTowns
    compileOnly ("io.github.fabiozumbi12.RedProtect:RedProtect-Core:8.1.1") { exclude(group: "*") }
    compileOnly ("io.github.fabiozumbi12.RedProtect:RedProtect-Spigot:8.1.1") { exclude(group: "*") }
    compileOnly ('com.bgsoftware:SuperiorSkyblockAPI:2023.3') { exclude(group: "*") }
    compileOnly ('dev.espi:protectionstones:2.10.2') { exclude(group: "*") }

    // Factions
    compileOnly ("com.github.SaberLLC:Saber-Factions:4.1.2-STABLE") { exclude(group: "*") }
    // Combat
    compileOnly ('com.github.sirblobman.api:core:2.9-SNAPSHOT') { exclude(group: "*") }
    compileOnly ('com.github.sirblobman.combatlogx:api:11.4-SNAPSHOT') { exclude(group: "*") }
    compileOnly ('com.github.timderspieler:DeluxeCombat-API:1.3.3') { exclude(group: "*") }

    // Maven-less Resources
    compileOnly files('../lib/Residence-5.1.4.1.jar') // https://github.com/Zrips/Residence
    compileOnly files('../lib/AdvancedEnchantmentsAPI-8.7.4.jar') // https://www.spigotmc.org/resources/advancedenchantments-api.76819/
    compileOnly files('../lib/FabledSkyBlock-3.0.0-b7-SNAPSHOT.jar') // https://github.com/craftaro/FabledSkyBlock/tree/development, CC BY-NC-ND 4.0
    compileOnly files('../lib/CrazyEnchantments-2.1.2.jar') // https://github.com/Crazy-Crew/CrazyEnchantments, MIT License
}

processResources {
    eachFile { details ->
        if (details.name.endsWith('.yml')) {
            filter {
                String line -> line.replaceAll('\\$\\{revision}', version.toString())
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    relocate 'org.bstats', 'org.spazzinq.flightcontrol'
    relocate 'com.flowpowered' , 'com.griefdefender.lib.flowpowered'
    archiveFileName = 'FlightControl.jar'
}
